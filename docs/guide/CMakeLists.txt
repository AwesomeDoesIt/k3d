PROJECT(guide)

FIND_PROGRAM(ASCIIDOC_COMMAND asciidoc)
FIND_PROGRAM(A2X_COMMAND a2x)
FIND_PROGRAM(QHELPGENERATOR_COMMAND NAMES qhelpgenerator qhelpgenerator-mac)
FIND_PROGRAM(QCOLLECTIONGENERATOR_COMMAND NAMES qcollectiongenerator qcollectiongenerator-mac)

SET(CONTENT_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/content)
SET(CONTENT_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/content)

FILE(MAKE_DIRECTORY ${CONTENT_BINARY_DIR})

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/index.txt.in ${CONTENT_BINARY_DIR}/index.txt @ONLY)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/index-docinfo.xml.in ${CONTENT_BINARY_DIR}/index-docinfo.xml @ONLY)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/plugins.py.in ${CMAKE_CURRENT_BINARY_DIR}/plugins.py @ONLY)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/guide.qhp.in ${CMAKE_CURRENT_BINARY_DIR}/guide.qhp @ONLY)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/guide.qhcp.in ${CMAKE_CURRENT_BINARY_DIR}/guide.qhcp @ONLY)

FILE(GLOB GUIDE_CONTENT ${CONTENT_SOURCE_DIR}/*.txt)
LIST(APPEND GUIDE_CONTENT ${CONTENT_BINARY_DIR}/index.txt)
LIST(APPEND GUIDE_CONTENT ${CONTENT_BINARY_DIR}/index-docinfo.xml)
LIST(APPEND GUIDE_CONTENT ${CONTENT_BINARY_DIR}/plugins.txt)
LIST(SORT GUIDE_CONTENT)

SET(K3D_NUI_BINARY ${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins/k3d-nui.module)
ADD_CUSTOM_COMMAND(
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/plugins.py
	WORKING_DIRECTORY ${CONTENT_BINARY_DIR}
	COMMAND
		${k3d_BINARY_DIR}/bin/k3d --log-level=debug --color --plugins=${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins --share=${k3d_SOURCE_DIR}/share --script-file=${CMAKE_CURRENT_BINARY_DIR}/plugins.py --ui=${K3D_NUI_BINARY}
	OUTPUT ${CONTENT_BINARY_DIR}/plugins.txt
	)

ADD_CUSTOM_COMMAND(
	DEPENDS ${GUIDE_CONTENT}
	COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_CURRENT_BINARY_DIR}/xml
	COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/xml
	COMMAND ${ASCIIDOC_COMMAND} --attribute docinfo --doctype book --backend docbook --out-file ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml ${CONTENT_BINARY_DIR}/index.txt
	OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml
	)

ADD_CUSTOM_TARGET(docs-guide-xml
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml
	)

ADD_CUSTOM_TARGET(docs-guide-html
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml
	COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_CURRENT_BINARY_DIR}/html
	COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/html
	COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_CURRENT_BINARY_DIR}/xml ${CMAKE_CURRENT_BINARY_DIR}/html
	COMMAND ${A2X_COMMAND} -v -f chunked ${CMAKE_CURRENT_BINARY_DIR}/html/index.xml
	)

ADD_CUSTOM_TARGET(docs-guide-pdf
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml
	COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_CURRENT_BINARY_DIR}/pdf
	COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/pdf
	COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_CURRENT_BINARY_DIR}/xml ${CMAKE_CURRENT_BINARY_DIR}/pdf
	COMMAND ${A2X_COMMAND} -v --destination-dir ${CMAKE_CURRENT_BINARY_DIR}/pdf -f pdf ${CMAKE_CURRENT_BINARY_DIR}/pdf/index.xml
	)

ADD_CUSTOM_COMMAND(
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/xml/index.xml ${CMAKE_CURRENT_BINARY_DIR}/guide.qhp
	COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_CURRENT_BINARY_DIR}/qt
	COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/qt
	COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_CURRENT_BINARY_DIR}/xml ${CMAKE_CURRENT_BINARY_DIR}/qt
	COMMAND ${A2X_COMMAND} -v -f chunked ${CMAKE_CURRENT_BINARY_DIR}/qt/index.xml
	COMMAND ${QHELPGENERATOR_COMMAND} ${CMAKE_CURRENT_BINARY_DIR}/guide.qhp -o ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qch
	OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qch
	)

ADD_CUSTOM_COMMAND(
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qch ${CMAKE_CURRENT_BINARY_DIR}/guide.qhcp
	COMMAND ${QCOLLECTIONGENERATOR_COMMAND} ${CMAKE_CURRENT_BINARY_DIR}/guide.qhcp -o ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qhc
	OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qhc
	)

ADD_CUSTOM_TARGET(docs-guide-qt
	DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/qt/guide.qhc
	)

