PROJECT(application)

INCLUDE_DIRECTORIES(${k3d_SOURCE_DIR})
INCLUDE_DIRECTORIES(${k3dsdk_BINARY_DIR})
INCLUDE_DIRECTORIES(${K3D_BOOST_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${K3D_GLIBMM_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${K3D_SIGC_INCLUDE_DIRS})

LINK_DIRECTORIES(${K3D_BOOST_LIB_DIRS})
LINK_DIRECTORIES(${K3D_SIGC_LIB_DIRS})

SET(SOURCES k3d_main.cpp)

IF(WIN32 AND MSVC)
	SET(SOURCES ${SOURCES} winmain.cpp)
ELSE(WIN32 AND MSVC)
	SET(SOURCES ${SOURCES} main.cpp)
ENDIF(WIN32 AND MSVC)

IF(APPLE)
	LIST(APPEND SOURCES "${CMAKE_SOURCE_DIR}/share/icons/k3d.icns")
	SET_SOURCE_FILES_PROPERTIES("${CMAKE_SOURCE_DIR}/share/icons/k3d.icns" PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")
ENDIF()

IF(WIN32)
	IF(MINGW)
		ADD_CUSTOM_COMMAND(
			OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/icon.o
			COMMAND
				windres.exe
				-I${CMAKE_CURRENT_SOURCE_DIR}
				-i${CMAKE_CURRENT_SOURCE_DIR}/icon.rc
				-o ${CMAKE_CURRENT_BINARY_DIR}/icon.o
			)

		SET(SOURCES ${SOURCES} ${CMAKE_CURRENT_BINARY_DIR}/icon.o)
	ELSE(MINGW)
		SET(SOURCES ${SOURCES} icon.rc)
	ENDIF(MINGW)
ENDIF(WIN32)

ADD_EXECUTABLE(k3d MACOSX_BUNDLE WIN32 ${SOURCES})
TARGET_LINK_LIBRARIES(k3d k3dsdk)
TARGET_LINK_LIBRARIES(k3d ${K3D_BOOST_PROGRAM_OPTIONS_LIBS})

IF(UNIX AND NOT APPLE)
	SET_TARGET_PROPERTIES(k3d PROPERTIES LINK_FLAGS -Wl,-E)
ENDIF()

IF(APPLE)
	SET_TARGET_PROPERTIES(k3d PROPERTIES MACOSX_BUNDLE_BUNDLE_NAME "K-3D")
	SET_TARGET_PROPERTIES(k3d PROPERTIES MACOSX_BUNDLE_BUNDLE_VERSION "@K3D_VERSION@")
	SET_TARGET_PROPERTIES(k3d PROPERTIES MACOSX_BUNDLE_COPYRIGHT "@K3D_COPYRIGHT@")
	SET_TARGET_PROPERTIES(k3d PROPERTIES MACOSX_BUNDLE_ICON_FILE "k3d")
	SET_TARGET_PROPERTIES(k3d PROPERTIES MACOSX_BUNDLE_INFO_STRING "K-3D @K3D_VERSION@, @K3D_COPYRIGHT@")
#	SET_TARGET_PROPERTIES(k3d PROPERTIES RESOURCE "${CMAKE_SOURCE_DIR}/share/icons/k3d.icns")
ENDIF()

IF(K3D_ENABLE_NLS)
	INCLUDE_DIRECTORIES(${K3D_INTL_INCLUDE_DIRS})
	TARGET_LINK_LIBRARIES(k3d ${K3D_INTL_LIBS})
ENDIF(K3D_ENABLE_NLS)

SET(K3D_NGUI_BINARY ${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins/k3d-ngui.module)
SET(K3D_NUI_BINARY ${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins/k3d-nui.module)
SET(K3D_PYUI_BINARY ${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins/k3d-pyui.module)
SET(K3D_QTUI_BINARY ${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins/k3d-qtui.module)

IF(APPLE)
	SET(K3D_BINARY ${k3d_BINARY_DIR}/bin/k3d.app/Contents/MacOS/k3d)
ELSE()
	SET(K3D_BINARY ${k3d_BINARY_DIR}/bin/k3d)
ENDIF()

SET(K3D_DEFAULT_ARGUMENTS "")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--color")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--log-level=debug")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--plugins=${k3d_BINARY_DIR}/${K3D_LIBDIR}/k3d/plugins")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--share=${k3d_SOURCE_DIR}/share")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--add-path=${k3d_BINARY_DIR}/bin")
LIST(APPEND K3D_DEFAULT_ARGUMENTS "--locale=${k3d_BINARY_DIR}/po")

SET(K3D_EXTRA_GDB_ARGUMENTS "" CACHE STRING "Extra arguments to pass to gdb for debugging.")

ADD_CUSTOM_TARGET(run-nui
	COMMAND ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_NUI_BINARY}
	)

ADD_CUSTOM_TARGET(run-gdb-nui
	COMMAND gdb ${K3D_EXTRA_GDB_ARGUMENTS} --args ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_NUI_BINARY}
	)

ADD_CUSTOM_TARGET(run-gdb-core
	COMMAND gdb ${K3D_EXTRA_GDB_ARGUMENTS} ${K3D_BINARY} core
	)

ADD_CUSTOM_TARGET(run-valgrind
	COMMAND valgrind -v ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS}
	)

ADD_CUSTOM_TARGET(run-strace
	COMMAND strace ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS}
	)

IF(K3D_BUILD_NGUI_MODULE)
	ADD_CUSTOM_TARGET(run-ngui
		COMMAND ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_NGUI_BINARY}
		)

	ADD_CUSTOM_TARGET(run-gdb-ngui
		COMMAND gdb ${K3D_EXTRA_GDB_ARGUMENTS} --args ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_NGUI_BINARY}
		)
ENDIF()

IF(K3D_BUILD_PYUI_MODULE)
	ADD_CUSTOM_TARGET(run-pyui
		COMMAND ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_PYUI_BINARY}
		)

	ADD_CUSTOM_TARGET(run-gdb-pyui
		COMMAND gdb ${K3D_EXTRA_GDB_ARGUMENTS} --args ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_PYUI_BINARY}
		)
ENDIF()

IF(K3D_BUILD_QTUI_MODULE)
	ADD_CUSTOM_TARGET(run
		COMMAND ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_QTUI_BINARY}
		)

	ADD_CUSTOM_TARGET(run-gdb
		COMMAND gdb --args ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_QTUI_BINARY}
		)

	ADD_CUSTOM_TARGET(run-qtui
		COMMAND ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_QTUI_BINARY}
		)

	ADD_CUSTOM_TARGET(run-gdb-qtui
		COMMAND gdb --args ${K3D_BINARY} ${K3D_DEFAULT_ARGUMENTS} --ui=${K3D_QTUI_BINARY}
		)
ENDIF()

INSTALL(TARGETS k3d DESTINATION bin)

